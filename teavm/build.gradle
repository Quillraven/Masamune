plugins {
  id 'java'
  id 'org.gretty' version '3.1.8'
}

gretty {
  contextPath = '/'
  extraResourceBase 'build/dist/webapp'
}

project.ext.mainClassName = 'io.github.masamune.teavm.TeaVMBuilder'
eclipse.project.name = appName + '-teavm'

// This must be at least 11, and no higher than the JDK version this project is built with.
java.targetCompatibility = "11"
// This should probably be equal to targetCompatibility, above. This only affects the TeaVM module.
java.sourceCompatibility = "11"


dependencies {
  implementation "com.badlogicgames.gdx:gdx-box2d-gwt:$gdxVersion"
  implementation "com.github.xpenatan.gdx-teavm:gdx-freetype-teavm:$gdxTeaVMVersion"
  implementation "com.github.xpenatan.gdx-teavm:backend-web:$gdxTeaVMVersion"
  implementation "com.github.raeleus.stripe:freetype:$stripeVersion:sources"
  implementation "com.github.raeleus.stripe:stripe:$stripeVersion:sources"
  implementation project(':core')

}

tasks.register('buildJavaScript', JavaExec) {
  setGroup("teavm")
  setDescription("Transpile bytecode to JavaScript via TeaVM")
  dependsOn classes
  mainClass.set(project.mainClassName)
  setClasspath(sourceSets.main.runtimeClasspath)
}
build.dependsOn buildJavaScript

tasks.register("run") {
  setGroup("teavm")
  setDescription("Run the JavaScript application hosted via a local Jetty server at http://localhost:8080/")
  dependsOn(buildJavaScript, tasks.named("jettyRun"))
}
